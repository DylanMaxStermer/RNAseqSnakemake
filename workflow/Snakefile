# The main entry point of your workflow.
# After configuring, running snakemake -n in a clone of this repository should successfully execute a dry-run of the workflow.
# Config file should have unique sample names for each sample. Rows with the
# same samples name will be merged (eg, for combining the same sample
# sequencing across multiple lanes with multiple sets of fastq)

# ============================================================================
# RNA-seq Snakemake Pipeline
# ============================================================================

configfile: "config/config.yaml"

# ============================================================================
# Modules and constraints
# ============================================================================

include: "rules/common.py"

wildcard_constraints:
    GenomeName = "|".join(STAR_genomes.index),
    sample = "|".join(AllSamples),
    Strandedness = "U|FR|RF",
    target = "|".join(config.get('liftover', {}).get('targets', {}).keys()),
    exon_type = "Constitutive|exon_flanking_PE_all|leafcutter_CE_gtf_matched|poisonExon_all"

# ============================================================================
# Rules
# ============================================================================

include: "rules/IndexGenome.smk"
include: "rules/PreprocessAndAlign.smk"
include: "rules/QC.smk"
include: "rules/ExpressionAnalysis.smk"
include: "rules/MakeBigwigs.smk"
#include: "rules/SplicingAnalysis.smk"
#include: "rules/Leafcutter2.smk"
include: "rules/SplicingLC2.smk"
include: "rules/ExonCharacteristic.smk"
include: "rules/LiftOver.smk"

# ============================================================================
# Targets
# ============================================================================

rule all:
    input:
        # Genomes
        rules.Gather_used_STAR_Indexes.input,
        
        # Alignments
        expand("results/Alignments/{sample}/Aligned.sortedByCoord.out.bam", sample=AllSamples),
        expand("results/Alignments/{sample}/Aligned.sortedByCoord.out.bam.indexing_done", sample=AllSamples),

        # Expression
        expand("results/featureCounts/{GenomeName}/AllSamplesUnstrandedCounting.Counts.txt", GenomeName = samples['STARGenomeName'].unique()),

        # Differential Expression with EdgeR
        #expand("differential_expression/{contrast}/results.tsv.gz", contrast=contrasts),
        #expand("ExpressionMatrices/{GenomeName}/log2TPM.bed", GenomeName = samples['STARGenomeName'].unique()),
        
        # QC
        "results/QC/ReadCountsPerSamples.tsv",
        "results/Multiqc",

        # Visualization with BigWigs
        expand("results/bigwigs/unstranded/{sample}.bw", sample=AllSamples),
        expand("results/bigwigs/grouped/{group}.bw", group=BigWigGroups) if BigWigGroups else [],

        # Splicing Analysis Using Leafcutter
        ## This will get junction counts and run the junction classifier from leafcutter2 
        expand("results/SplicingAnalysis/leafcutter/{GenomeName}/juncTableBeds/{Metric}.sorted.bed.gz", GenomeName = samples['STARGenomeName'].unique(), Metric = ["JuncCounts", "PSI", "PSI_ByMax", "PSIDenom", "PSI_ByMaxDenom"]),
        expand("results/SplicingAnalysis/ClassifyJuncs/{GenomeName}/Leaf2_junction_classifications.txt", GenomeName = samples['STARGenomeName'].unique()),
        ### This will add splice site scores to the splice sits ID'd with leafcutter. It does this by comparing to a PWM based on all annotated 5'/3'ss
        expand("results/SplicingAnalysis/ObservedJuncsAnnotations/{GenomeName}.uniq.annotated.with_ss_scores.tsv.gz", GenomeName = samples['STARGenomeName'].unique()),

        # Differential Splicing With Leafcutter
        expand("results/SplicingAnalysis/differential_splicing_tidy/{contrast}/Results.tsv.gz", contrast=contrasts),

        # Leafcutter2 Core Outputs
        expand("results/SplicingAnalysis/leafcutter2/{GenomeName}/leafcutter2.cluster_ratios.const.gz", GenomeName = samples['STARGenomeName'].unique()),
        expand("results/SplicingAnalysis/leafcutter2/{GenomeName}/leafcutter2.junction_counts.const.gz", GenomeName = samples['STARGenomeName'].unique()),
        expand("results/SplicingAnalysis/leafcutter2/{GenomeName}/clustering/leafcutter2_junction_classifications.txt", GenomeName = samples['STARGenomeName'].unique()),

        # Unproductive Event Detection (Poison Junctions)
        ## Poison Exons - Non-coding exons flanked by coding exons
        expand("results/SplicingAnalysis/PoisonEvent/Exon/PE/{GenomeName}_coding_noncoding_introns_all.tsv",
               GenomeName=samples['STARGenomeName'].unique()),

        ## Poison Donors/Acceptors - Alternative splice sites creating non-coding junctions
        expand("results/SplicingAnalysis/PoisonEvent/DonorAcceptor/PoisonDonorAcceptor/{GenomeName}_poisonDonorAcceptor.bed",
               GenomeName=samples['STARGenomeName'].unique()),

        ## Poison Skipped - Non-coding junctions spanning from CDS to CDS (exon skipping)
        expand("results/SplicingAnalysis/PoisonEvent/Skipped/PoisonSkipped/{GenomeName}_poisonSkipped.bed",
               GenomeName=samples['STARGenomeName'].unique()),

        # Exon Characteristics Analysis
        ## Conservation analysis plots (phyloP and phastCons)
        ## NOTE: requires DS-filtered exon types — uncomment when differential splicing data is available
        #expand("results/ExonCharacteristics/{GenomeName}/phyloP_phastCons/plots/phyloP_phase0_comparison_mean.pdf",
        #       GenomeName=samples['STARGenomeName'].unique()),
        #expand("results/ExonCharacteristics/{GenomeName}/phyloP_phastCons/plots/phastCons_phase0_comparison_mean.pdf",
        #       GenomeName=samples['STARGenomeName'].unique()),

        ## Constitutive exons from single-intron Leafcutter2 clusters
        expand("results/SplicingAnalysis/productive/Constitutive/{GenomeName}/constitutive_exons.bed",
               GenomeName=samples['STARGenomeName'].unique()),
        expand("results/ExonCharacteristics/{GenomeName}/StandardFormat/Constitutive/exons_std.tsv",
               GenomeName=samples['STARGenomeName'].unique()),

        ## Productive alternative donor/acceptor junctions
        expand("results/SplicingAnalysis/productive/ProductiveDonorAcceptor/{GenomeName}_altDonorAcceptor.bed",
               GenomeName=samples['STARGenomeName'].unique()),
        expand("results/SplicingAnalysis/productive/ProductiveDonorAcceptor/flankingCDS/{GenomeName}_altDonorAcceptor_flankingCDS.tsv",
               GenomeName=samples['STARGenomeName'].unique()),

        ## Poison donor/acceptor flanking CDS
        expand("results/SplicingAnalysis/PoisonEvent/DonorAcceptor/PoisonDonorAcceptor/flankingCDS/{GenomeName}_poisonDonorAcceptor_flankingCDS.tsv",
               GenomeName=samples['STARGenomeName'].unique()),

        ## Alternate donor/acceptor splice site distance distribution plots
        expand("results/ExonCharacteristics/plots/{GenomeName}/AltDonorAcceptor/distribution/donor/counts/overlay_comparison.png",
               GenomeName=samples['STARGenomeName'].unique()),

        ## Conservation scores for constitutive exons (no DS filtering required)
        expand("results/ExonCharacteristics/{GenomeName}/phyloP_phastCons/Constitutive/phyloP35way_phastCons35way_summary.tsv",
               GenomeName=samples['STARGenomeName'].unique()),

        ## MaxEnt splice site scoring for exon types
        expand("results/ExonCharacteristics/{GenomeName}/SpliceSites/{exon_type}/donor_sites_scored.bed",
               GenomeName=samples['STARGenomeName'].unique(),
               exon_type=["leafcutter_CE_gtf_matched", "poisonExon_all",
                          "exon_flanking_PE_all", "Constitutive"]),
        expand("results/ExonCharacteristics/{GenomeName}/SpliceSites/{exon_type}/acceptor_sites_scored.bed",
               GenomeName=samples['STARGenomeName'].unique(),
               exon_type=["leafcutter_CE_gtf_matched", "poisonExon_all",
                          "exon_flanking_PE_all", "Constitutive"]),

        # Reciprocal LiftOver — orthologous exon mapping to other species
        expand("results/liftOver/{GenomeName}/{exon_type}/{target}/reciprocal/hg38_to_{target}_reciprocal_lifted.tsv",
               GenomeName=samples['STARGenomeName'].unique(),
               exon_type=["Constitutive", "exon_flanking_PE_all", "leafcutter_CE_gtf_matched", "poisonExon_all"],
               target=list(config.get('liftover', {}).get('targets', {}).keys())),







        


rule Gather_Fastp_Fastqs:
    input:
        expand("FastqFastp/{sample}.fastp.html", sample=AllSamples),





        # expand("differential_expression/{contrast}/results.tsv.gz", contrast=contrasts),